#+AUTHOR: Jeff Mataya
#+TITLE: Jeff's Emacs Configuration

* Table of Contents                                                :TOC:
- [[#bootstrap][Bootstrap]]
- [[#environment][Environment]]
- [[#theme][Theme]]
  - [[#color-scheme][Color Scheme]]
  - [[#font][Font]]
- [[#vim-key-bindings][Vim Key Bindings]]
  - [[#the-basics][The Basics]]
  - [[#window-navigation][Window Navigation]]
- [[#file-and-buffer-navigation][File and Buffer Navigation]]
  - [[#fuzzy-matching][Fuzzy Matching]]
  - [[#tree-view][Tree View]]
  - [[#buffers][Buffers]]
- [[#coding-preferences][Coding Preferences]]
  - [[#commenting-code][Commenting Code]]
  - [[#git][Git]]
  - [[#tags][Tags]]
  - [[#text][Text]]
  - [[#visual][Visual]]
- [[#languages][Languages]]
  - [[#the-web][The Web]]
  - [[#clojure][Clojure]]
  - [[#css][CSS]]
  - [[#docker][Docker]]
  - [[#go][Go]]
  - [[#javascript][JavaScript]]
  - [[#markdown][Markdown]]
  - [[#org][Org]]
  - [[#php][PHP]]
  - [[#vue][Vue]]
- [[#preferences][Preferences]]
  - [[#modeline][Modeline]]
  - [[#navigation][Navigation]]
- [[#credit][Credit]]
- [[#license][License]]

* Bootstrap

Modify default UI elements to be less invasive.

#+BEGIN_SRC emacs-lisp
(blink-cursor-mode 0)                             ; Disable cursor blinking
(scroll-bar-mode 0)                               ; Hide scroll bars
(tool-bar-mode 0)                                 ; Remove the toolbars
(tooltip-mode 0)                                  ; No more graphical tooltips
#+END_SRC

Set defaults so that we start with a sane system and build on top.

#+BEGIN_SRC emacs-lisp
(setq-default
 auto-window-vscroll nil                          ; Lighten vertical scroll
 column-number-mode t                             ; Display current column in the modeline
 confirm-kill-emacs 'yes-or-no-p                  ; Confirm before exiting Emacs
 cursor-in-non-selected-windows t                 ; Hide the cursor in inactive windows
 delete-by-moving-to-trash t                      ; Delete files to trash
 display-time-default-load-average nil            ; Don't display load average
 display-time-format "%H:%M"                      ; Format the time string
 help-window-select t                             ; Focus new help windows when opened
 indent-tabs-mode nil                             ; Don't indent using tabs
 initial-scratch-message ""                       ; Get rid of the usual scratch message
 inhibit-startup-message t                        ; Get rid of the usual startup message
 line-number-mode t                               ; Display line numbers
 ; ns-use-srgb-colorspace nil                       ; Fix colorspace for MacOS
 scroll-conservatively most-positive-fixnum       ; Always scroll by one line
 select-enable-clipboard t                        ; Merge system's and Emacs' clipboard
 sentence-end-double-space nil                    ; End a sentence after a dot and a space
 show-trailing-whitespace nil                     ; Display trailing whitespaces
 split-height-threshold nil                       ; Disable vertical window splitting
 split-width-threshold nil                        ; Disable horizontal window splitting
 tab-width 4                                      ; Set width for tabs
 uniquify-buffer-name-style 'forward              ; Uniquify buffer names
 visual-bell t                                    ; Bell is visual not auditory
 window-combination-resize t                      ; Resize windows proportionally
 x-stretch-cursor t)                              ; Stretch cursor to the glyph width
(display-time-mode 1)                             ; Enable time in the modeline
(fset 'yes-or-no-p 'y-or-n-p)                     ; Ask y/n instead of yes/no
(global-auto-revert-mode t)                       ; Automatically load changes from disk
(global-linum-mode t)                             ; Display line numbers everywhere
(setq linum-format "%4d ")                        ; Align numbers nicely
(global-subword-mode 1)                           ; Iterate through CamelCase words
(menu-bar-mode 0)                                 ; Disable the menu bar
#+END_SRC

Enable garbage collection on focus-out, hopefully makes Emacs /feel/ snappier.

#+BEGIN_SRC emacs-lisp
(add-hook 'focus-out-hook #'garbage-collect)
#+END_SRC

Make sure that =use-package= is available, as it will be used everywhere.

#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
#+END_SRC

* Environment

Setup MacOS-specific layout settings.

#+BEGIN_SRC emacs-lisp
(setq-default
  exec-path (append exec-path '("/usr/local/bin"))  ; Add homebrew path
  ns-option-modifier 'meta                         ; Map Meta to the Alt key
  ns-right-option-modifier nil)                    ; Disable the right Alt key
#+END_SRC

Initialize environment variables.

#+BEGIN_SRC emacs-lisp
(use-package exec-path-from-shell
  :ensure t)

(when (memq window-system '(mac ns x))
  (exec-path-from-shell-initialize))
#+END_SRC

* Theme
  
** Color Scheme

Remove the setting where italic text is underlined if no slanted face exists.

#+BEGIN_SRC emacs-lisp
(set-face-attribute 'italic nil :underline nil)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package dracula-theme
  :demand t
  :ensure t
  :config
  (load-theme 'dracula t))
  
(set-face-attribute 'fringe nil
                      :foreground (face-foreground 'default)
                      :background (face-background 'default))
                  
#+END_SRC

** Font

My current flavor of the moment in terms of font face is [[https://www.typography.com/fonts/operator/styles/][Operator Mono]]. It
has beautiful readability and displays italics in a cursive font, which is
especially nice in JavaScript frameworks like React. It unfortunately has two
downsides:

1. It's pretty expensive
2. It doesn't support ligatures

I get around this issue by using fontsets to use [[https://github.com/tonsky/FiraCode][Fira Code]] as a fallback and
specifically set up its ligatures.

*** Font Face
    
Configure Fira Code as the fallback font and Operator Mono as the default. Also,
check whether Emacs is running on MacOS or another operating system. This is a
simple hack to determine whether this is a HiDPI screen or not and is based on
my personal hardware.

#+BEGIN_SRC emacs-lisp
(use-package dash :ensure t)

(defun format-font (face size)
  "Format a string describing a font face and size."
  (format "%s-%d" face size))

(defun font-candidate (&rest fonts)
  "Return the first available font."
  (--first (find-font (font-spec :name it)) fonts))

(defun jeff-initialize-font (&rest frame)
  "Set the frame font with the first available font."
  (when window-system
    (if (eq system-type 'darwin)
      (setq jeff-font-size 16)
      (setq jeff-font-size 14))
    (set-frame-font (font-candidate
                      (format-font "Operator Mono SSm" jeff-font-size)
                      (format-font "Fira Code" jeff-font-size)))))
    
(when window-system (jeff-initialize-font))
(add-hook 'after-make-frame-functions 'jeff-initialize-font t)
#+END_SRC

*** Ligatures

All of this code sets up ligatures based on the [[https://github.com/tonsky/FiraCode/files/412440/FiraCode-Regular-Symbol.zip][Fira Code Symbol]] font. It has a
few disabled ligatures, such as ones that interfere with things like org mode
headers.

Credit to [[https://github.com/ekaschalk/.spacemacs.d/blob/master/layers/display/local/pretty-fonts/pretty-fonts.el][@ekaschalk]] for this solution.

#+BEGIN_SRC emacs-lisp
(use-package dash :ensure t)
(provide 'pretty-fonts)

;;; API
(when window-system

  ;;;###autoload
  (defun pretty-fonts-set-fontsets (CODE-FONT-ALIST)
    "Utility to associate many unicode points with specified fonts."
    (--each CODE-FONT-ALIST
      (-let (((font . codes) it))
        (--each codes
          (set-fontset-font nil `(,it . ,it) font)
          (set-fontset-font t `(,it . ,it) font)))))
  
  ;;;###autoload
  (defun pretty-fonts--add-kwds (FONT-LOCK-ALIST)
    "Exploits `font-lock-add-keywords' to apply regex-unicode replacements."
    (font-lock-add-keywords
     nil (--map (-let (((rgx uni-point) it))
                 `(,rgx (0 (progn
                             (compose-region
                              (match-beginning 1) (match-end 1)
                              ,(concat "\t" (list uni-point)))
                             nil))))
               FONT-LOCK-ALIST)))
  
  ;;;###autoload
  (defmacro pretty-fonts-set-kwds (FONT-LOCK-HOOKS-ALIST)
    "Set regex-unicode replacements to many modes."
    `(--each ,FONT-LOCK-HOOKS-ALIST
       (-let (((font-locks . mode-hooks) it))
         (--each mode-hooks
           (add-hook it (-partial 'pretty-fonts--add-kwds
                                  (symbol-value font-locks)))))))
  
  ;;; Fira Font
  
  (defconst pretty-fonts-fira-font
    '(;; OPERATORS
      ;; Pipes
      ("\\(<|\\)" #Xe14d) ("\\(<>\\)" #Xe15b) ("\\(<|>\\)" #Xe14e) ("\\(|>\\)" #Xe135)
  
      ;; Brackets
      ("\\(<\\*\\)" #Xe14b) ("\\(<\\*>\\)" #Xe14c) ("\\(\\*>\\)" #Xe104)
      ("\\(<\\$\\)" #Xe14f) ("\\(<\\$>\\)" #Xe150) ("\\(\\$>\\)" #Xe137)
      ("\\(<\\+\\)" #Xe155) ("\\(<\\+>\\)" #Xe156) ("\\(\\+>\\)" #Xe13a)
  
      ;; Equality
      ("\\(!=\\)" #Xe10e) ("\\(!==\\)"         #Xe10f) ("\\(=/=\\)" #Xe143)
      ("\\(/=\\)" #Xe12c) ("\\(/==\\)"         #Xe12d)
      ;; ("\\(===\\)"#Xe13d) ("[^!/]\\(==\\)[^>]" #Xe13c)
  
      ;; Equality Special
      ("\\(||=\\)"  #Xe133) ("[^|]\\(|=\\)" #Xe134)
      ("\\(~=\\)"   #Xe166)
      ("\\(\\^=\\)" #Xe136)
      ("\\(=:=\\)"  #Xe13b)
  
      ;; Comparisons
      ("\\(<=\\)" #Xe141) ("\\(>=\\)" #Xe145)
      ("\\(</\\)" #Xe162) ("\\(</>\\)" #Xe163)
  
      ;; Shifts
      ("[^-=]\\(>>\\)" #Xe147) ("\\(>>>\\)" #Xe14a)
      ("[^-=]\\(<<\\)" #Xe15c) ("\\(<<<\\)" #Xe15f)
  
      ;; Dots
      ("\\(\\.-\\)"    #Xe122) ("\\(\\.=\\)" #Xe123)
      ("\\(\\.\\.<\\)" #Xe125)
  
      ;; Hashes
      ("\\(#{\\)"  #Xe119) ("\\(#(\\)"   #Xe11e) ("\\(#_\\)"   #Xe120)
      ("\\(#_(\\)" #Xe121) ("\\(#\\?\\)" #Xe11f) ("\\(#\\[\\)" #Xe11a)
  
      ;; REPEATED CHARACTERS
      ;; 2-Repeats
      ("\\(||\\)" #Xe132)
      ("\\(!!\\)" #Xe10d)
      ("\\(%%\\)" #Xe16a)
      ("\\(&&\\)" #Xe131)
  
      ;; 2+3-Repeats
      ("\\(##\\)"       #Xe11b) ("\\(###\\)"         #Xe11c) ("\\(####\\)" #Xe11d)
      ;; ("\\(--\\)"       #Xe111) ("\\(---\\)"         #Xe112)
      ("\\({-\\)"       #Xe108) ("\\(-}\\)"          #Xe110)
      ("\\(\\\\\\\\\\)" #Xe106) ("\\(\\\\\\\\\\\\\\)" #Xe107)
      ("\\(\\.\\.\\)"   #Xe124) ("\\(\\.\\.\\.\\)"   #Xe126)
      ("\\(\\+\\+\\)"   #Xe138) ("\\(\\+\\+\\+\\)"   #Xe139)
      ("\\(//\\)"       #Xe12f) ("\\(///\\)"         #Xe130)
      ("\\(::\\)"       #Xe10a) ("\\(:::\\)"         #Xe10b)
  
      ;; ARROWS
      ;; Direct
      ("[^-]\\(->\\)" #Xe114) ("[^=]\\(=>\\)" #Xe13f)
      ("\\(<-\\)"     #Xe152)
      ("\\(-->\\)"    #Xe113) ("\\(->>\\)"    #Xe115)
      ("\\(==>\\)"    #Xe13e) ("\\(=>>\\)"    #Xe140)
      ("\\(<--\\)"    #Xe153) ("\\(<<-\\)"    #Xe15d)
      ("\\(<==\\)"    #Xe158) ("\\(<<=\\)"    #Xe15e)
      ("\\(<->\\)"    #Xe154) ("\\(<=>\\)"    #Xe159)
  
      ;; Branches
      ("\\(-<\\)"  #Xe116) ("\\(-<<\\)" #Xe117)
      ("\\(>-\\)"  #Xe144) ("\\(>>-\\)" #Xe148)
      ("\\(=<<\\)" #Xe142) ("\\(>>=\\)" #Xe149)
      ("\\(>=>\\)" #Xe146) ("\\(<=<\\)" #Xe15a)
  
      ;; Squiggly
      ("\\(<~\\)" #Xe160) ("\\(<~~\\)" #Xe161)
      ("\\(~>\\)" #Xe167) ("\\(~~>\\)" #Xe169)
      ("\\(-~\\)" #Xe118) ("\\(~-\\)"  #Xe165)
  
      ;; MISC
      ("\\(www\\)"                   #Xe100)
      ("\\(<!--\\)"                  #Xe151)
      ("\\(~@\\)"                    #Xe164)
      ("[^<]\\(~~\\)"                #Xe168)
      ("\\(\\?=\\)"                  #Xe127)
      ("[^=]\\(:=\\)"                #Xe10c)
      ("\\(/>\\)"                    #Xe12e)
      ("[^\\+<>]\\(\\+\\)[^\\+<>]"   #Xe16d)
      ("[^:=]\\(:\\)[^:=]"           #Xe16c)
      ("\\(<=\\)"                    #Xe157))
    "Fira font ligatures and their regexes")
  
  (set-fontset-font t '(#Xe100 . #Xe16f) "Fira Code Symbol")
  
  (pretty-fonts-set-kwds
    '((pretty-fonts-fira-font prog-mode-hook org-mode-hook))))
#+END_SRC

* Vim Key Bindings

Emacs is an amazing work environment, but I can't get by without Vim key
bindings. This section has only the most basic setup and window navigation
settings. Key bindings that are related to specific modes will be located
with that mode's configuration.

** The Basics

Get the basics up and running: evil, support for key chords (so insert mode can
be exited with =jj=, among other things), and support for a leader key.

#+BEGIN_SRC emacs-lisp
(setq evil-want-C-i-jump nil)

(use-package evil
  :ensure t)

(use-package evil-leader
  :ensure t
  :after evil)
  
(use-package key-chord
  :ensure t)

(evil-mode 1)                                     ; Enable evil (Vim) mode

(global-evil-leader-mode)                         ; Emulate Vim's leader key
(evil-leader/set-leader ",")                      ; Set ',' as the leader key

(key-chord-mode 1)                                ; Map commands to key chords
(setq
 key-chord-two-key-delay 0.7                      ; Timeout for recognizing a two-key chord
 key-chord-one-key-delay 1.0)                     ; Timeout after first key chord
(key-chord-define 
  evil-insert-state-map "jk" 'evil-normal-state)  ; "jk" exists insert mode
#+END_SRC

** Window Navigation

Key mapping for manipulating windows and frames.

| Command | Action                                                         |
|---------+----------------------------------------------------------------|
| =vv=    | Split the current window into two vertically stacked windows   |
| =ss=    | Split the current window into two horizontally stacked windows |
| =,1=    | Close all windows except for the current one                   |
| =,v=    | Close all windows in the current vertical split                |
| =,q=    | Save and close the current window                              |
| =C-h=   | Navigate to the window left of the current window              |
| =C-l=   | Navigate to the window right of the current window             |
| =C-k=   | Navigate to the window above of the current window             |
| =C-j=   | Navigate to the window below of the current window             |
| =,m=    | Maximized the current frame                                    |
| =,f=    | Toggle fullscreen for the current frame                        |

#+BEGIN_SRC emacs-lisp
(eval-after-load "evil"
  '(progn
    (define-key evil-normal-state-map (kbd "C-h") 'evil-window-left)
    (define-key evil-normal-state-map (kbd "C-j") 'evil-window-down)
    (define-key evil-normal-state-map (kbd "C-k") 'evil-window-up)
    (define-key evil-normal-state-map (kbd "C-l") 'evil-window-right)))

(key-chord-define evil-normal-state-map "vv" 'split-window-horizontally)
(key-chord-define evil-normal-state-map "ss" 'split-window-vertically)

(evil-leader/set-key
  "m" 'toggle-frame-maximized
  "f" 'toggle-frame-fullscreen
  "v" 'delete-other-windows-vertically
  "1" 'delete-other-windows
  "q" 'evil-save-and-close)
#+END_SRC

* File and Buffer Navigation

This configuration supports navigating the environment by using fuzzy matching
and a project tree. Both work inside of source control and support custom buffer
matching.

| Command             | Action                                         |
|---------------------+------------------------------------------------|
| =,b=                | View all open buffers                          |
| =C-p=               | Open fuzzy matching pane                       |
| =,p=                | Open project matching pane                     |
| =C-n=               | Open file browser                              |
| =C-c p n= / =s-p n= | Add a project so that it can be searched again |

** Fuzzy Matching

Helm and Projectile are used for fuzzy matching when searching for files and
navigating menus.

#+BEGIN_SRC emacs-lisp
(use-package projectile :ensure t :diminish t)
(use-package helm :ensure t)
(use-package helm-projectile :ensure t)
(use-package helm-ag :ensure t)

(projectile-mode +1)
(require 'helm-config)
(define-key projectile-mode-map (kbd "s-p") 'projectile-command-map)
(define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
(define-key projectile-mode-map (kbd "s-p n") 'projectile-add-known-project)
(define-key projectile-mode-map (kbd "C-c p n") 'projectile-add-known-project)
(define-key projectile-mode-map (kbd "C-c p f") 'helm-projectile-ag)

(eval-after-load "evil"
  '(progn
    (define-key evil-normal-state-map (kbd "C-p") 'helm-projectile)))

(evil-leader/set-key
  "p" 'helm-projectile-switch-project
  "f" 'helm-projectile-ag)
  
(setq projectile-enable-caching t)
#+END_SRC

Look for projects under my default coding directory (=~/code=).

#+BEGIN_SRC emacs-lisp
(setq projectile-project-search-path '("~/code/hacking"))
#+END_SRC

** Tree View

Visually, I find that I need a tree to keep everything organized, especially for
large projects.

After using Atom for a while, I've become partial to the fancy icon UI to
identify folder types. So, the =all-the-icons= package is being installed to
give us a little eye candy.

#+BEGIN_SRC emacs-lisp
(use-package neotree
  :ensure t
  :pin melpa-stable)

(setq neo-autorefresh nil)

(use-package all-the-icons
  :ensure t
  :pin melpa-stable)

(setq neo-theme (if (display-graphic-p) 'icons 'arrow))

(defun neotree-project-dir ()
    "Open NeoTree using the git root."
    (interactive)
    (let ((project-dir (projectile-project-root))
          (file-name (buffer-file-name)))
      (neotree-toggle)
      (if project-dir
          (if (neo-global--window-exists-p)
              (progn
                (neotree-dir project-dir)
                (neotree-find file-name)))
        (message "Could not find git project root."))))

(eval-after-load "evil"
  '(progn
    (define-key evil-normal-state-map (kbd "C-n") 'neotree-project-dir)))

(add-hook 'neotree-mode-hook
  (lambda ()
    (define-key evil-normal-state-local-map (kbd "TAB") 'neotree-enter)
    (define-key evil-normal-state-local-map (kbd "SPC") 'neotree-enter)
    (define-key evil-normal-state-local-map (kbd "q") 'neotree-hide)
    (define-key evil-normal-state-local-map (kbd "RET") 'neotree-enter)))
#+END_SRC

** Buffers

Use the much superior =iBuffer= to navigate buffer lists and Helm to quickly
search through buffers.

#+BEGIN_SRC emacs-lisp
(require 'ibuffer)
(global-set-key (kbd "C-x C-b") 'ibuffer)
(setq ibuffer-default-sorting-mode 'major-mode)

(evil-leader/set-key "b" 'helm-buffers-list)
(global-set-key (kbd "C-x b") 'helm-buffers-list)
(setq-default dired-listing-switches "-alh")
#+END_SRC

* Coding Preferences
  
| Command | Action          |
|---------+-----------------|
| =,g=    | View Git status |

** Commenting Code

I love tpope's awesome vim-commentary plugin for Vim. Bringing over the
behavior to emacs (via evil).

#+BEGIN_SRC emacs-lisp
(use-package evil-commentary
  :diminish evil-commentary-mode
  :ensure t)

(evil-commentary-mode)
#+END_SRC

** Git

Magit is pretty much the best git plugin available.

#+BEGIN_SRC emacs-lisp
(use-package magit :ensure t)
(evil-leader/set-key "g" 'magit-status)
(global-set-key (kbd "C-c C-g") 'magit-status)
#+END_SRC

** Tags

Make use of tags to so that we can navigate the code.

#+BEGIN_SRC emacs-lisp
(use-package ggtags :ensure t)
(setq path-to-ctags "/usr/local/bin/ctags")

(defun create-tags (dir-name)
  "Create tags file."
  (interactive "Directory: ")
  (shell-command
    (format "%s -f TAGS -e -R %s" path-to-ctags (directory-file-name dir-name))))

(evil-leader/set-key
  "l d" 'ggtags-find-definition)
#+END_SRC

** Text

#+BEGIN_SRC emacs-lisp
(setq-default fill-column 80)
#+END_SRC
** Visual

Hide line numbers where they are unnecessary or cause performance issues.

#+BEGIN_SRC emacs-lisp
(defun nolinum ()
  (linum-mode 0))

(add-hook 'org-mode-hook 'nolinum)
(add-hook 'shell-mode-hook 'nolinum)
(add-hook 'ansi-term-hook 'nolinum)
(add-hook 'term-mode-hook 'nolinum)
#+END_SRC

Visually indicate matching parens

#+BEGIN_SRC emacs-lisp
(show-paren-mode t)
(setq show-paren-delay 0.0)
#+END_SRC

* Languages

I prefer to be as idiomatic as possible for any language that write, so call 
configuration sits here.

** The Web

So much of the coding that I do is on the web, so setup the defaults for
web-mode, which may be overwritten by specific web-mode engine coding styles.

#+BEGIN_SRC emacs-lisp
(use-package web-mode :ensure t)
(setq web-mode-code-indent-offset 4)
(setq web-mode-indent-style 4)
#+END_SRC

** Clojure

#+BEGIN_SRC emacs-lisp
(use-package paredit :ensure t)
(use-package clojure-mode :ensure t)
(use-package clojure-mode-extra-font-locking :ensure t)
(use-package cider :ensure t)
(use-package rainbow-delimiters :ensure t)

;; Automatically load paredit when editing a lisp file
;; More at http://www.emacswiki.org/emacs/ParEdit
(autoload 'enable-paredit-mode "paredit" "Turn on pseudo-structural editing of Lisp code." t)
(add-hook 'emacs-lisp-mode-hook       #'enable-paredit-mode)
(add-hook 'eval-expression-minibuffer-setup-hook #'enable-paredit-mode)
(add-hook 'ielm-mode-hook             #'enable-paredit-mode)
(add-hook 'lisp-mode-hook             #'enable-paredit-mode)
(add-hook 'lisp-interaction-mode-hook #'enable-paredit-mode)
(add-hook 'scheme-mode-hook           #'enable-paredit-mode)

;; eldoc-mode shows documentation in the minibuffer when writing code
;; http://www.emacswiki.org/emacs/ElDoc
(add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)
(add-hook 'lisp-interaction-mode-hook 'turn-on-eldoc-mode)
(add-hook 'ielm-mode-hook 'turn-on-eldoc-mode)

;;;;
;; Clojure
;;;;

;; Enable paredit for Clojure
(add-hook 'clojure-mode-hook 'enable-paredit-mode)

;; This is useful for working with camel-case tokens, like names of
;; Java classes (e.g. JavaClassName)
(add-hook 'clojure-mode-hook 'subword-mode)

;; A little more syntax highlighting
(require 'clojure-mode-extra-font-locking)

;; syntax hilighting for midje
(add-hook 'clojure-mode-hook
          (lambda ()
            (setq inferior-lisp-program "lein repl")
            (font-lock-add-keywords
             nil
             '(("(\\(facts?\\)"
                (1 font-lock-keyword-face))
               ("(\\(background?\\)"
                (1 font-lock-keyword-face))))
            (define-clojure-indent (fact 1))
            (define-clojure-indent (facts 1))
            (rainbow-delimiters-mode)))

;;;;
;; Cider
;;;;

;; provides minibuffer documentation for the code you're typing into the repl
(add-hook 'cider-mode-hook 'eldoc-mode)

;; go right to the REPL buffer when it's finished connecting
(setq cider-repl-pop-to-buffer-on-connect t)

;; When there's a cider error, show its buffer and switch to it
(setq cider-show-error-buffer t)
(setq cider-auto-select-error-buffer t)

;; Where to store the cider history.
(setq cider-repl-history-file "~/.emacs.d/cider-history")

;; Wrap when navigating history.
(setq cider-repl-wrap-history t)

;; enable paredit in your REPL
(add-hook 'cider-repl-mode-hook 'paredit-mode)

;; Use clojure mode for other extensions
(add-to-list 'auto-mode-alist '("\\.edn$" . clojure-mode))
(add-to-list 'auto-mode-alist '("\\.boot$" . clojure-mode))
(add-to-list 'auto-mode-alist '("\\.cljs.*$" . clojure-mode))
(add-to-list 'auto-mode-alist '("lein-env" . enh-ruby-mode))

;; key bindings
;; these help me out with the way I usually develop web apps
(defun cider-start-http-server ()
  (interactive)
  (cider-load-current-buffer)
  (let ((ns (cider-current-ns)))
    (cider-repl-set-ns ns)
    (cider-interactive-eval (format "(println '(def server (%s/start))) (println 'server)" ns))
    (cider-interactive-eval (format "(def server (%s/start)) (println server)" ns))))


(defun cider-refresh ()
  (interactive)
  (cider-interactive-eval (format "(user/reset)")))

(defun cider-user-ns ()
  (interactive)
  (cider-repl-set-ns "user"))

(eval-after-load 'cider
  '(progn
     (define-key clojure-mode-map (kbd "C-c C-v") 'cider-start-http-server)
     (define-key clojure-mode-map (kbd "C-M-r") 'cider-refresh)
     (define-key clojure-mode-map (kbd "C-c u") 'cider-user-ns)
     (define-key cider-mode-map (kbd "C-c u") 'cider-user-ns)))
#+END_SRC

** CSS

#+BEGIN_SRC emacs-lisp
(use-package css-mode
  :ensure t
  :config (setq-default css-indent-offset 2))

(use-package scss-mode
  :ensure t
  :preface
  (defun jeff/scss-set-comment-style ()
    (setq-local comment-end "")
    (setq-local comment-start "//"))
  :delight scss-mode "SCSS"
  :mode ("\\.sass\\'" "\\.scss\\'")
  :hook (scss-mode . jeff/scss-set-comment-style))
#+END_SRC

** Docker

#+BEGIN_SRC emacs-lisp
(use-package dockerfile-mode
  :ensure t
  :delight dockerfile-mode "Dockerfile"
  :mode "Dockerfile\\'")
#+END_SRC

** Go

#+BEGIN_SRC emacs-lisp
(use-package go-mode :ensure t)
#+END_SRC

Run =goimports= to format and import packages on save.

#+BEGIN_SRC emacs-lisp
(setq gofmt-command "goimports")
(add-hook 'before-save-hook 'gofmt-before-save)
#+END_SRC

Define a couple commands for compiling and testing generic Go projects.

For compile: =go build && go vet=

#+BEGIN_SRC emacs-lisp
(use-package gotest :ensure t)

(defun go-compile-hook ()
  (if (not (string-match "go" compile-command))
      (set (make-local-variable 'compile-command)
           "go build -v && go vet")))

(add-hook 'go-mode-hook 'go-compile-hook)
#+END_SRC

Add key bindings for compiling, testing, and going to definition.

| Command | Action                                    |
|---------+-------------------------------------------|
| =gd=    | Jump to the a method or type's definition |
| =,c=    | Compile the current project               |
| =,h=    | Test the current project                  |

#+BEGIN_SRC emacs-lisp
(add-hook 'go-mode-hook (lambda()
  (evil-leader/set-key "c" 'compile)
  (evil-leader/set-key "h" 'go-test-current-project)
  (key-chord-define evil-normal-state-map "gd" 'godef-jump)))
#+END_SRC

** JavaScript

#+BEGIN_SRC emacs-lisp
(use-package js2-mode :ensure t)
(add-hook 'js2-mode-hook (lambda () (setq js2-basic-offset 2)))
(setq indent-tabs-mode nil
      js-indent-level 2)

(add-to-list 'auto-mode-alist '("\\.js[x]?\\'" . js2-mode))
(add-to-list 'auto-mode-alist '("\\.json\\'" . js2-mode))
#+END_SRC

** Markdown

#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :ensure t
  :commands (markdown-mode gfm-mode)
  :mode (("\\.md\\'" . gfm-mode)
         ("\\.markdown\\'" . markdown-mode)))
#+END_SRC

** Org
   
#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure t
  :delight org-mode "Org"
  :preface
  (defun jeff/org-src-buffer-name (org-buffer-name language)
    "Construct the buffer name for a source editing buffer. See
    `org-src--construct-edit-buffer-name'."
    (format "*%s*" org-buffer-name))
  :config
  (setq-default
   org-support-shift-select 'always
   org-startup-truncated nil))
#+END_SRC

Make Org a bit easier to use with Evil while in normal mode.

| Command | Action                                    |
|---------+-------------------------------------------|
| ,d      | Jump to the a method or type's definition |
| ,t      | Cycle through todo states                 |
| ,a      | Bring up agenda view                      |

#+BEGIN_SRC emacs-lisp
(defun org-keymap-hook ()
  (evil-leader/set-key
    "t" 'org-todo
    "d" 'org-open-at-point))

(add-hook 'org-mode-hook 'org-keymap-hook)
(evil-leader/set-key
  "a" 'org-agenda)
#+END_SRC

Org is a great place for task management. Let's make it all a bit more
friendly by giving some better state names and appending a timestamp when tasks
are completed.

#+BEGIN_SRC emacs-lisp
(setq org-todo-keywords
  '((sequence "TODO" "IN-PROGRESS" "BLOCKED" "|" "DONE" "CANCELED")))
  
(defun jeff-org-skip-subtree-if-priority (priority)
  "Skip an agenda subtree if it has a priority of PRIORITY
  
PRIORITY may be one of the characters ?A, ?B, or ?C."
  (let ((subtree-end (save-excursion (org-end-of-subtree t)))
        (pri-value (* 1000 (- org-lowest-priority priority)))
        (pri-current (org-get-priority (thing-at-point 'line t))))
    (if (= pri-value pri-current)
        subtree-end
      nil)))
      
(defun jeff-org-skip-subtree-if-habit ()
  "Skip an agenda entry if it has a STYLE property equal to \"habit\"."
  (let ((subtree-end (save-excursion (org-end-of-subtree t))))
    (if (string= (org-entry-get nil "STYLE") "habit")
        subtree-end
      nil)))
      
(defun jeff-org-task-capture ()
  "Capture a task with my default template."
  (interactive)
  (org-capture nil "a"))

(setq org-agenda-custom-commands
      '(("d" "Daily agenda and all TODOs"
         ((tags "PRIORITY=\"A\""
                ((org-agenda-skip-function '(org-agenda-skip-entry-if 'todo 'done))
                 (org-agenda-overriding-header "High-priority unfinished tasks:")))
          (agenda "" ((org-agenda-ndays 1)))
          (alltodo ""
                   ((org-agenda-skip-function '(or (jeff-org-skip-subtree-if-habit)
                                                   (jeff-org-skip-subtree-if-priority ?A)
                                                   (org-agenda-skip-if nil '(scheduled deadline))))
                    (org-agenda-overriding-header "ALL normal priority tasks:")))))))
                    
(setq org-capture-templates
  '(("a" "My TODO task format." entry
     (file "~/notes/todo.org")
     "* TODO %?
SCHEDULED: %t")))

(define-key global-map (kbd "C-c c") 'jeff-org-task-capture)

(setq org-log-done 'time)
#+END_SRC

It's great to be able to use Emacs as an authoring system and export to
all kinds of formats.

#+BEGIN_SRC emacs-lisp
(eval-after-load "org"
  '(require 'ox-md nil t))                        ; Allow exporting markdown
#+END_SRC

A cool little hack I found: automatically maintain TOC headers.

#+BEGIN_SRC emacs-lisp
(use-package toc-org
  :ensure t
  :after org)
  
(add-hook 'org-mode-hook 'toc-org-enable)
#+END_SRC

Set up the agenda search path so we can use agenda in TODO:

#+BEGIN_SRC emacs-lisp
(setq org-agenda-files '("~/notes/"))
#+END_SRC

I like sexy looking bullets rather than just having a trail of ASCII stars.

#+BEGIN_SRC emacs-lisp
(use-package org-bullets
  :ensure t
  :pin melpa-stable)

(require 'org-bullets)
(add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
#+END_SRC

Make sure that indentation works correctly with src blocks.

#+BEGIN_SRC emacs-lisp
(setq org-src-tab-acts-natively t)
#+END_SRC

** PHP

#+BEGIN_SRC emacs-lisp
(use-package web-mode :ensure t)
(add-to-list 'auto-mode-alist '("\\.php\\'" . web-mode))
(add-to-list 'web-mode-engines-alist '("php" . "\\.phtml\\'"))
(add-to-list 'web-mode-engines-alist '("blade" . "\\.blade\\."))

(add-hook 'web-mode-hook
  (lambda ()
    (ggtags-mode 1)))
#+END_SRC

** Vue

#+BEGIN_SRC emacs-lisp
(use-package vue-mode
  :ensure t
  :config
  (setq mmm-submode-decoration-level 0))
#+END_SRC

#+BEGIN_SRC emacs-lisp 
(use-package yaml-mode :ensure t)
#+END_SRC

* Preferences
  
** Modeline
   
*** Spaceline

 #+BEGIN_SRC emacs-lisp
; (setq-default
;   powerline-height 30
;   powerline-default-separator nil
;   spaceline-flycheck-bullet "❖ %s"
;   spaceline-separator-dir-left '(right . right)
;   spaceline-separator-dir-right '(left . left))
; 
; (setq x-underline-at-descent-line t)
; (use-package spaceline :ensure t)
; (require 'spaceline-config)
; (spaceline-spacemacs-theme)
; (setq spaceline-highlight-face-func 'spaceline-highlight-face-evil-state)
; (setq-default powerline-default-separator 'nil)
 #+END_SRC
 
*** Hide Minor Modes

A lot of the minor modes that display don't do anything to help readability or
anything productive. So, let's hide things!

#+BEGIN_SRC emacs-lisp
(use-package diminish :ensure t)
(diminish 'projectile-mode)
(diminish 'evil-commentary-mode)
(diminish 'evil-leader-mode)
(diminish 'undo-tree-mode)
(diminish 'subword-mode)
#+END_SRC

*** Custom Modeline

#+BEGIN_SRC emacs-lisp
;;   (defun trim-string (string)
;;     "Remove white spaces in beginning and ending of STRING.
;;   White space here is any of: space, tab, emacs newline (line feed, ASCII 10)."
;;   (replace-regexp-in-string "\\`[ \t\n]*" "" (replace-regexp-in-string "[ \t\n]*\\'" "" string)))
;; 
;;   (defun fox-extract-number (str)
;;     (car (split-string str " ")))
;; 
;;   (defun fox-cmd-git-current-branch ()
;;     "Retreives the name of the current git branch for the current buffer."
;;     (shell-command-to-string "git rev-parse --abbrev-ref HEAD"))
;; 
;;   (defun fox-cmd-git-change-summary ()
;;     "Get the number of files changed, insertions, and deletions."
;;     (shell-command-to-string "git diff --stat | grep changed"))
;; 
;;   (defun fox-git-change-line ()
;;     "Gets the summary of changes."
;;     (pcase-let ((
;;                  `(,ch ,up ,down)
;;                  (mapcar
;;                   'fox-extract-number
;;                   (split-string
;;                    (trim-string (fox-cmd-git-change-summary))
;;                    ", "))))
;;       (if (equal ch "")
;;           (propertize (format ""))
;;         (concat
;;           (propertize (format " • %s" (all-the-icons-faicon "refresh"))
;;                       'face `(:height 1 :family ,(all-the-icons-faicon-family))
;;                       'display '(raise 0))
;;           (propertize (format " %s" ch))
;;           (if (or (equal up nil) (equal up ""))
;;               (propertize (format ""))
;;             (concat
;;               (propertize (format " %s" (all-the-icons-faicon "arrow-circle-o-up"))
;;                           'face `(:height 1 :family ,(all-the-icons-faicon-family))
;;                           'display '(raise 0))
;;               (propertize (format " %s" up))))
;;           (if (or (equal down nil) (equal down ""))
;;               (propertize (format ""))
;;             (concat
;;               (propertize (format " %s" (all-the-icons-faicon "arrow-circle-o-down"))
;;                           'face `(:height 1 :family ,(all-the-icons-faicon-family))
;;                           'display '(raise 0))
;;               (propertize (format " %s" down))))))))
;;     
;;   (defun fox-git-branch ()
;;     "Gets the git branch for the current buffer."
;;     (let ((branch (fox-cmd-git-current-branch))
;;           (changes (mapcar
;;                     'fox-extract-number
;;                     (cdr (split-string (fox-cmd-git-change-summary) ", ")))))
;;       (concat
;;        (propertize (format " • %s" (all-the-icons-octicon "git-branch"))
;;                    'face `(:height 1 :family ,(all-the-icons-octicon-family))
;;                    'display '(raise 0))
;;        (propertize (format " %s" (trim-string branch)))
;;        (fox-git-change-line))))
;; 
;;   (defvar fox-mode-vc
;;     '(:propertize
;;       (:eval (when vc-mode
;;                (cond
;;                 ((string-match "Git[:-]" vc-mode) (fox-git-branch))
;;                 (t (format "%s" vc-mode)))))
;;       face mode-line-directory)
;;     "Formats the modeline version control string.")
;; 
;;   (defun fox-mode-evil-state ()
;;     "Formats the Evil state for the modeline."
;;       (when evil-mode-line-tag
;;         (cond
;;           ((string-match "<N>" evil-mode-line-tag) (format " %s" "NORMAL"))
;;           ((string-match "<I>" evil-mode-line-tag) (format " %s" "INSERT"))
;;           ((string-match "<V>" evil-mode-line-tag) (format " %s" "VISUAL"))
;;           ((string-match "<R>" evil-mode-line-tag) (format " %s" "REPLACE"))
;;           ((string-match "<O>" evil-mode-line-tag) (format " %s" "OPERATOR-PENDING"))
;;           ((string-match "<M>" evil-mode-line-tag) (format " %s" "MOTION"))
;;           ((string-match "<E>" evil-mode-line-tag) (format " %s" "EMACS"))
;;           (t (format "%s" evil-mode-line-tag)))))
;; 
;;   (defvar fox-mode-evil
;;     '(:propertize
;;       (:eval (fox-mode-evil-state))
;;       face mode-line-directory))
;; 
;;   (defun fox-mode-file-description ()
;;     "Gets the current filename with it's corresponding icon."
;;     (concat
;;       (propertize (format " • %s" (all-the-icons-icon-for-file (buffer-file-name))
;;                   'face `(:height 1 :family ,(all-the-icons-fileicon-family))
;;                   'display '(raise 0)))
;;       (propertize (format " %s" (buffer-name)))))
;; 
;;   (defvar fox-mode-file
;;     '(:propertize
;;       (:eval (fox-mode-file-description))
;;       face mode-line-directory))
;; 
;;   (setq-default mode-line-format
;;                 (list
;;                  fox-mode-evil
;;                  fox-mode-file
;;                  " "
;;                  mode-line-modified
;;                  fox-mode-vc
;;                  " • "
;;                  mode-line-position))
#+END_SRC

** Navigation

#+BEGIN_SRC emacs-lisp
(global-set-key [C-left] 'windmove-left)          ; move to left window
(global-set-key [C-right] 'windmove-right)        ; move to right window
(global-set-key [C-up] 'windmove-up)              ; move to upper window
(global-set-key [C-down] 'windmove-down)          ; move to lower window
#+END_SRC

* Credit

This configuration is inspired by the follow people and projects:

- [[https://github.com/angrybacon/dotemacs][angrybacon/dotemacs]]
- [[https://github.com/ekaschalk/.spacemacs.d/blob/master/layers/display/local/pretty-fonts/pretty-fonts.el][ekaschalk/.spacemacs.d]]

* License

GPLv3
